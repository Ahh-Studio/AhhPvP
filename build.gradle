plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

base {
	archivesName = project.archives_base_name
}

version = "$project.version"
group = project.maven_group

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url "https://maven.fabricmc.net/"
        name "Fabric Maven"
    }
    maven {
        url "https://maven.quiltmc.org/repository/release/"
        name "Quilt Maven"
    }
    maven {
        name "Terraformers"
        url "https://maven.terraformersmc.com/"
    }
}

loom {
	mods {
		"pvp" {
			sourceSet("main")
		}
	}
}

dependencies {
	minecraft libs.minecraft
    mappings loom.layered {
        mappings "org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:intermediary-v2"
        mappings "${libs.yarn.mappings.get()}:v2"
    }
	modImplementation libs.quilt.loader
	modImplementation libs.bundles.quilted.fabric.api
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
}

processResources {
	inputs.properties 'version': version, 'group': project.group

	filesMatching('quilt.mod.json') {
		expand 'version': version, 'group': project.group
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	withSourcesJar()
}

jar {
	from('LICENSE') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
